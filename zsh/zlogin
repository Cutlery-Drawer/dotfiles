#
# Executes commands at login post-zshrc.
#

# Execute code that does not affect the current session in the background.
{
  # Compile the completion dump to increase startup speed.
  zcompdump="${ZDOTDIR:-$HOME}/.zcompdump"

  if [[ -s "${zcompdump}" && (! -s "${zcompdump}.zwc" || "${zcompdump}" -nt "${zcompdump}.zwc") ]]; then
    zcompile "${zcompdump}"
  fi

  # Set environment variables for launchd processes.
  if [[ "${OSTYPE}" == darwin* ]]; then
    for env_var in PATH MANPATH; do
      launchctl setenv "${env_var}" "${(P)env_var}" 2>/dev/null
    done
  fi
} &!

# Print an ESV bible verse.
if (( $+commands[devotion] )); then
  devotion

# Print a random, hopefully interesting, adage.
elif (( $+commands[fortune] )); then
  if [[ -t 0 || -t 1 ]]; then

    # KJV bible.
    if [[ -f ~/.config/fortune/bible-kjv/bible ]]; then
      fortune ~/.config/fortune/bible-kjv/bible

    # Standard fortune, but short ones only.
    else
      fortune -s
    fi

    print
  fi
fi

# vim: set filetype=zsh:
